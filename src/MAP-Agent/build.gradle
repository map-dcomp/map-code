import org.gradle.internal.os.OperatingSystem

// Plugins
plugins {
    id 'java-library'
    id "com.gorylenko.gradle-git-properties" version "1.5.2"
    id 'application'    
    id "com.github.ben-manes.versions" version "0.21.0"
}
apply plugin: 'com.github.johnrengelman.shadow'

def props = new Properties()
file("../P2Protelis/gradle.properties").withInputStream { props.load(it) }
file("../gradle.common.properties").withInputStream { props.load(it) }

group = 'com.bbn.map'
version = props.getProperty("map_lofi_version")

mainClassName = 'com.bbn.map.simulator.SimulationRunner'

description = """MAP Agent"""

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

configurations {
    providedRuntime
    
    all {
        exclude group: 'ch.qos.logback'
    }
}

dependencies {
    api project(':P2Protelis')

    api group: 'org.slf4j', name: 'slf4j-api', version: props.getProperty("slf4j_version")
    runtimeOnly group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: props.getProperty("log4j_version")
    api group: 'org.apache.logging.log4j', name: 'log4j-api', version: props.getProperty("log4j_version")
    api group: 'org.apache.logging.log4j', name: 'log4j-core', version: props.getProperty("log4j_version")
    api group: 'org.apache.logging.log4j', name: 'log4j-jcl', version: props.getProperty("log4j_version")

    api group: 'com.google.guava', name: 'guava', version: props.getProperty("guava_version")
    api group: 'org.projectlombok', name: 'lombok', version: props.getProperty("lombok_version")

    api group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: props.getProperty("jackson_version")
    api group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: props.getProperty("jackson_version")
    api group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: props.getProperty("jackson_version")
    api group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml', version: props.getProperty("jackson_version")
    api group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-guava', version: props.getProperty("jackson_version")
    api group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: props.getProperty("jackson_version")
    api group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: props.getProperty("jackson_version")
    api group: 'com.fasterxml.jackson.module', name: 'jackson-module-parameter-names', version: props.getProperty("jackson_version")

    api group: 'org.apache.httpcomponents', name: 'fluent-hc', version: props.getProperty("apache_hc_version")
    
    api group: 'org.jacop', name: 'jacop', version: '4.6.0', {
      exclude group: 'org.slf4j', module: 'slf4j-api'
      exclude group: 'org.slf4j', module: 'slf4j-log4j12'
      exclude group: 'org.mockito', module: 'mockito-all'
      exclude group: 'org.scala-lang', module: 'scala-library'
      exclude group: 'org.scala-lang', module: 'scala-compiler'
    }
    api group: 'org.jfree', name: 'jfreechart', version: '1.5.0'
    api group: 'com.diffplug.durian', name: 'durian', version: props.getProperty("durian_version")
    
    api(group: 'org.reflections', name: 'reflections', version: '0.9.12')    
        
    testCompile project (path: ":P2Protelis", configuration: 'testArtifacts')
    
    testImplementation group: 'org.assertj', name: 'assertj-core', version: props.getProperty("assertj_core_version")
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: props.getProperty("hamcrest_version")
    testImplementation( group: 'org.xmlmatchers', name: 'xml-matchers', version: props.getProperty("xml_matchers_version")) {
        exclude module: 'hamcrest-core'
    }    
    testImplementation( group: 'junit', name: 'junit', version: props.getProperty("junit_version")) {
        exclude module: 'hamcrest-core'
    }
    testImplementation( group: 'org.mockito', name: 'mockito-core', version: props.getProperty("mockito_version")) {
        exclude module: 'hamcrest-core'
    }
    
}

checkstyle {
    configDirectory = file("$projectDir")
    configFile = file("style.xml")
    showViolations = true
    ignoreFailures = true
    toolVersion = "8.18"
}

task sourceJar(type: Jar) {
  classifier "sources"
  from sourceSets.main.allJava
}

publishing {
    repositories {
        mavenLocal()
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
}

jar {
  baseName = "map-sim-runner"
}

gitProperties {
    gitPropertiesDir = "${buildDir}/resources/main/com/bbn/map/simulator"
}

shadowJar {
    baseName = "map-sim-runner"
    classifier = 'executable'
    
    // for some reason including the Log4j2 dat file causes problems
    // excluding the file causes log4j4 to scan for plugins and everything works 
    exclude 'META-INF/org/apache/logging/log4j/core/config/plugins/Log4j2Plugins.dat'
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-+]*/
            }
            if (rejected) {
               selection.reject('Release candidate')
            }
        }
    }
}
