import org.gradle.internal.os.OperatingSystem

buildscript {
  repositories {
      jcenter()
  }
}

// Plugins
plugins {
    id 'java'
    id 'maven'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'checkstyle'
    id 'findbugs'
    id 'com.dorongold.task-tree' version '1.3'
    id "com.gorylenko.gradle-git-properties" version "1.5.2"
    id 'com.github.johnrengelman.shadow' version '4.0.3'
    id 'application'    
}

def props = new Properties()
file("../gradle.common.properties").withInputStream { props.load(it) }

group = 'com.bbn.map'
version = props.getProperty("map_agent_version")

mainClassName = 'com.bbn.map.simulator.SimulationRunner'

description = """MAP Agent"""

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

repositories {
  // local repository first
  mavenLocal()
  
  jcenter()
}

configurations {
    providedRuntime
    all*.exclude group: 'ch.qos.logback'
}

dependencies {
    compile group: 'com.bbn.protelis', name: 'P2Protelis', version: '4.0.5'

    compile group: 'org.slf4j', name: 'slf4j-api', version: props.getProperty("slf4j_version")
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: props.getProperty("log4j_version")
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: props.getProperty("log4j_version")
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: props.getProperty("log4j_version")
    compile group: 'org.apache.logging.log4j', name: 'log4j-jcl', version: props.getProperty("log4j_version")

    compile group: 'com.google.guava', name: 'guava', version: props.getProperty("guava_version")
    compile group: 'org.projectlombok', name: 'lombok', version: props.getProperty("lombok_version")

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: props.getProperty("jackson_version")
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: props.getProperty("jackson_version")
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: props.getProperty("jackson_version")
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml', version: props.getProperty("jackson_version")
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-guava', version: props.getProperty("jackson_version")
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: props.getProperty("jackson_version")
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: props.getProperty("jackson_version")
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-parameter-names', version: props.getProperty("jackson_version")

    compile group: 'org.apache.httpcomponents', name: 'fluent-hc', version: props.getProperty("apache_hc_version")
    
    compile group: 'com.diffplug.durian', name: 'durian', version: '2.0'
    
    compile group: 'org.jacop', name: 'jacop', version: '4.5.1', {
      exclude group: 'org.slf4j', module: 'slf4j-api'
      exclude group: 'org.slf4j', module: 'slf4j-log4j12'
      exclude group: 'org.mockito', module: 'mockito-all'
      exclude group: 'org.scala-lang', module: 'scala-library'
      exclude group: 'org.scala-lang', module: 'scala-compiler'
    }
    compile group: 'org.jfree', name: 'jfreechart', version: '1.0.19'
    compile group: 'com.diffplug.durian', name: 'durian', version: props.getProperty("durian_version")
    
    
    testCompile group: 'org.assertj', name: 'assertj-core', version: props.getProperty("assertj_core_version")
    testCompile group: 'org.hamcrest', name: 'hamcrest', version: props.getProperty("hamcrest_version")
    testCompile group: 'org.xmlmatchers', name: 'xml-matchers', version: props.getProperty("xml_matchers_version")
    testCompile( group: 'junit', name: 'junit', version: props.getProperty("junit_version")) {
        exclude module: 'hamcrest-core'
    }
    testCompile( group: 'org.mockito', name: 'mockito-core', version: props.getProperty("mockito_version")) {
        exclude module: 'hamcrest-core'
    }
    
}

// set gradle version when generating the wrapper
task wrapper(type: Wrapper) {
     gradleVersion = '4.0'
}

eclipse {
    classpath {
        downloadSources = true
        downloadJavadoc = true
    }
}

test {
    testLogging.showStandardStreams = true
    ignoreFailures Boolean.getBoolean("test.ignoreFailures")
}

checkstyle {
    config = resources.text.fromFile('style.xml', 'UTF-8')
    showViolations = true
    ignoreFailures = true
    toolVersion = "8.10"
}

findbugs {
    ignoreFailures = true
}

task sourceJar(type: Jar) {
  classifier "sources"
  from sourceSets.main.allJava
}

publishing {
    repositories {
        mavenLocal()
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
}

jar {
  baseName = "map-sim-runner"
}

gitProperties {
    gitPropertiesDir = "${buildDir}/resources/main/com/bbn/map/simulator"
}

shadowJar {
    baseName = "map-sim-runner"
    classifier = 'executable'
    
    // for some reason including the Log4j2 dat file causes problems
    // excluding the file causes log4j4 to scan for plugins and everything works 
    exclude 'META-INF/org/apache/logging/log4j/core/config/plugins/Log4j2Plugins.dat'
 }
