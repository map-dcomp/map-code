import org.gradle.internal.os.OperatingSystem

buildscript {
  repositories {
      jcenter()
  }
}

// Plugins
plugins {
    id 'java'
    id 'maven'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'checkstyle'
    id 'findbugs'
    id 'com.dorongold.task-tree' version '1.3'
    id 'com.github.johnrengelman.shadow' version '4.0.3'
    id 'application'            
}

def props = new Properties()
file("../gradle.common.properties").withInputStream { props.load(it) }

group = 'com.bbn.map'
version = '0.1.0'

description = """MAP Demand Generation GUI"""
mainClassName = 'com.bbn.map.DemandGenerationGUI.Main'

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

repositories {
  // local repository first
  mavenLocal()
  
  jcenter()
}

configurations {
    providedRuntime
    all*.exclude group: 'ch.qos.logback'
}

dependencies {
    compile group: 'com.bbn.map', name: 'MAP-Agent', version: props.getProperty("map_agent_version")
    compile group: 'org.apache.commons', name: 'commons-csv', version: props.getProperty("commons_csv_version")
    compile group: 'commons-cli', name: 'commons-cli', version: props.getProperty("commons_cli_version")
    
    testCompile group: 'org.hamcrest', name: 'hamcrest', version: props.getProperty("hamcrest_version")
    testCompile( group: 'junit', name: 'junit', version: props.getProperty("junit_version")) {
        exclude module: 'hamcrest-core'
    }
}

// set gradle version when generating the wrapper
task wrapper(type: Wrapper) {
     gradleVersion = '4.0'
}

test {
    testLogging.showStandardStreams = true
}

checkstyle {
    config = resources.text.fromFile('style.xml', 'UTF-8')
    showViolations = true
    ignoreFailures = false
    toolVersion = "8.10"
}

findbugs {
    ignoreFailures = true
}

task sourceJar(type: Jar) {
  classifier "sources"
  from sourceSets.main.allJava
}

publishing {
    repositories {
        mavenLocal()
    }
    
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
}

shadowJar {
    classifier = 'executable'
    
    // for some reason including the Log4j2 dat file causes problems
    // excluding the file causes log4j4 to scan for plugins and everything works 
    exclude 'META-INF/org/apache/logging/log4j/core/config/plugins/Log4j2Plugins.dat'
 }
